1.	Напишите запрос для поиска всех фильмов с участием "Morgan Freeman".

PREFIX imdb: <http://academy.ontotext.com/imdb/>
PREFIX schema: <http://schema.org/>

SELECT ?title WHERE {
    ?movie a ?type ;
        schema:actor <http://academy.ontotext.com/imdb/person/MorganFreeman> ;
        schema:name ?title .
    FILTER (?type = imdb:ColorMovie || ?type = imdb:BlackandWhiteMovie)
}


2. Все цветные фильмы с участием Джонни Деппа в жанре драма

PREFIX imdb: <http://academy.ontotext.com/imdb/>
PREFIX schema: <http://schema.org/>

SELECT ?title (GROUP_CONCAT(?genre; SEPARATOR=", ") AS ?genres) WHERE {
    ?movie a imdb:ColorMovie ;
        schema:actor <http://academy.ontotext.com/imdb/person/JohnnyDepp> ;
        schema:name ?title ;
        schema:genre ?genre .
    FILTER EXISTS {
        ?movie schema:genre ?dramaGenre .
        FILTER (CONTAINS(LCASE(?dramaGenre), "drama"))
    }
}
GROUP BY ?movie ?title


3.	Выведите все фильмы, которые сняты в жанре фэнтези.

PREFIX imdb: <http://academy.ontotext.com/imdb/>
PREFIX schema: <http://schema.org/>

SELECT ?title WHERE {
    ?movie a ?type ;
        schema:genre ?genre ;
        schema:name ?title .
    FILTER (CONTAINS(LCASE(?genre), "fantasy"))
    FILTER (?type = imdb:ColorMovie || ?type = imdb:BlackandWhiteMovie)
}


4. Фильмы, снятые в США с количеством комментариев больше 500

PREFIX imdb: <http://academy.ontotext.com/imdb/>
PREFIX schema: <http://schema.org/>

SELECT ?title ?commentCount WHERE {
    ?movie a ?type ;
        schema:name ?title ;
        schema:countryOfOrigin <http://academy.ontotext.com/imdb/country/USA> ;
        schema:commentCount ?commentCount .
    FILTER (?commentCount > 500)
    FILTER (?type = imdb:ColorMovie || ?type = imdb:BlackandWhiteMovie)
}


5. Фильмы, где режиссер также является актером

PREFIX imdb: <http://academy.ontotext.com/imdb/>
PREFIX schema: <http://schema.org/>

SELECT ?title ?directorName WHERE {
    ?movie a ?type ;
        schema:name ?title ;
        schema:director ?director ;
        schema:actor ?director .
    ?director schema:name ?directorName .
    FILTER (?type = imdb:ColorMovie || ?type = imdb:BlackandWhiteMovie)
}
